#version 450

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

uniform uint maxSurfels;
uniform uint frameIndex;
uniform uint maxAgeFrames;

struct Surfel
{
    vec4 posRadius;
    vec4 normal;
    vec4 albedo;
    uvec4 meta; // x=lastUsedFrame, y=active (1/0)
};

layout(std430, binding = 0) buffer SurfelBuffer
{
    Surfel surfels[];
};

layout(std430, binding = 1) buffer CounterBuffer
{
    int stackTop;
    int pad0;
    int pad1;
    int pad2;
};

layout(std430, binding = 2) buffer FreeStackBuffer
{
    uint freeIds[];
};

void PushFree(uint surfelId)
{
    int idx = atomicAdd(stackTop, 1);
    if (idx >= 0 && uint(idx) < maxSurfels)
        freeIds[uint(idx)] = surfelId;
}

void main()
{
    uint id = gl_GlobalInvocationID.x;
    if (id >= maxSurfels)
        return;

    // Quick check
    if (surfels[id].meta.y == 0u)
        return;

    uint lastUsed = surfels[id].meta.x;
    uint age = frameIndex - lastUsed;
    if (age <= maxAgeFrames)
        return;

    // Deactivate exactly once.
    uint prev = atomicCompSwap(surfels[id].meta.y, 1u, 0u);
    if (prev == 1u)
        PushFree(id);
}
