#version 460 core

layout(local_size_x = 256) in;

const uint COMMAND_FLOATS = 48u;
const uint KEY_UINTS = 4u;

layout(std430, binding = 0) readonly buffer CulledCommands { float culled[]; };
layout(std430, binding = 1) readonly buffer CulledCountBuffer
{
    uint CulledCount;
    uint CulledInstanceCount;
    uint CulledOverflow;
};
layout(std430, binding = 2) writeonly buffer SortKeyBuffer { uint sortKeys[]; };

uniform int CurrentRenderPass;
uniform int MaxSortKeys;
uniform uint StateBitMask;
uniform int SortDomain;
uniform int SortDirection;

const int SORT_DOMAIN_MATERIAL_STATE = 0;
const int SORT_DOMAIN_OPAQUE_FRONT_TO_BACK = 1;
const int SORT_DOMAIN_TRANSPARENT_BACK_TO_FRONT = 2;

uint EncodeDistanceKey(float renderDistance)
{
    uint raw = floatBitsToUint(max(renderDistance, 0.0));
    if (SortDirection != 0)
        return ~raw;

    return raw;
}

void main()
{
    uint logicalIdx = gl_GlobalInvocationID.x;
    uint total = CulledCount;

    if (logicalIdx >= total || logicalIdx >= uint(MaxSortKeys))
        return;

    uint base = logicalIdx * COMMAND_FLOATS;
    if (base + COMMAND_FLOATS > uint(culled.length()))
        return;

    uint renderPass = floatBitsToUint(culled[base + 40u]);
    if (CurrentRenderPass >= 0 &&
        renderPass != uint(CurrentRenderPass) &&
        renderPass != 0xFFFFFFFFu)
    {
        return;
    }

    uint materialID = floatBitsToUint(culled[base + 38u]);
    uint meshID = floatBitsToUint(culled[base + 36u]);
    uint shaderProgramID = floatBitsToUint(culled[base + 41u]);
    float renderDistance = culled[base + 42u];
    uint flags = floatBitsToUint(culled[base + 45u]);

    uint stateBits = flags & StateBitMask;
    uint packedPassPipelineState =
        ((renderPass & 0xFFu) << 24u) |
        ((shaderProgramID & 0x0FFFu) << 12u) |
        (stateBits & 0x0FFFu);

    uint primarySortKey;
    uint secondarySortKey;

    if (SortDomain == SORT_DOMAIN_OPAQUE_FRONT_TO_BACK ||
        SortDomain == SORT_DOMAIN_TRANSPARENT_BACK_TO_FRONT)
    {
        primarySortKey = EncodeDistanceKey(renderDistance);
        secondarySortKey = materialID;
    }
    else
    {
        primarySortKey = materialID;
        secondarySortKey = meshID;
    }

    uint outBase = logicalIdx * KEY_UINTS;
    sortKeys[outBase + 0u] = packedPassPipelineState;
    sortKeys[outBase + 1u] = primarySortKey;
    sortKeys[outBase + 2u] = secondarySortKey;
    sortKeys[outBase + 3u] = logicalIdx;
}
